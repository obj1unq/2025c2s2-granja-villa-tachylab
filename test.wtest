import personaje.*
import cultivos.*
import wollok.game.*
import aspersorYPosiciones.*
import mercados.*

describe "Test de la siembra de cultivos" {
    test "Testeo de la siembra: Maiz" {
        personaje.sembrar(maiz)
        assert.equals("Maiz", game.uniqueCollider(personaje).nombre())
    }

    test "Testeo de la siembra: Trigo" {
        personaje.sembrar(trigo)
        assert.equals("Trigo", game.uniqueCollider(personaje).nombre())
    }

    test "Testeo de la siembra: Tomaco" {
        personaje.sembrar(tomaco)
        assert.equals("Tomaco", game.uniqueCollider(personaje).nombre())
    }

    test "Testeo de la siembra: No se puede sembrar" {
        personaje.sembrar(maiz)
        assert.throwsException({personaje.sembrar(maiz)})
    }
}

describe "Test del riego de cultivos"{
    test "Test de riego: Maiz" {
        personaje.sembrar(maiz)
        personaje.regar()
        assert.equals(adult, game.uniqueCollider(personaje).estado())
        personaje.regar()
        assert.equals(adult, game.uniqueCollider(personaje).estado())
    }

    test "Test de riego: Trigo" {
        personaje.sembrar(trigo)
        personaje.regar()
        assert.equals(1, game.uniqueCollider(personaje).etapa())
        personaje.regar()
        assert.equals(2, game.uniqueCollider(personaje).etapa())
        personaje.regar()
        assert.equals(3, game.uniqueCollider(personaje).etapa())
        personaje.regar()
        assert.equals(0, game.uniqueCollider(personaje).etapa())
    }
    
    test "Test de riego: Tomaco" {
        personaje.sembrar(tomaco)
        personaje.regar()
        personaje.position(personaje.position().up(1))
        assert.throwsException({ personaje.sembrar(tomaco) })
    }

    test "Test de riego: Tomaco caso limite" {
        personaje.position(game.at(0,game.height() - 1))
        personaje.sembrar(tomaco)
        personaje.regar()
        personaje.position(game.at(0,0))
        assert.throwsException({ personaje.sembrar(tomaco) })
    }
    test "Test de riego: No hay cultivo" {
        assert.throwsException({ personaje.regar() })
    }
}

describe "Test de la cosecha de cultivos" {
    test "Test de cosecha: Maiz" {
        personaje.sembrar(maiz)
        personaje.cosechar()
        assert.equals("Maiz", game.uniqueCollider(personaje).nombre())
        personaje.regar()
        personaje.cosechar()
        assert.that(game.colliders(personaje).isEmpty())
    }

    test "Test de cosecha: Trigo" {
        personaje.sembrar(trigo)
        personaje.cosechar()
        assert.equals("Trigo", game.uniqueCollider(personaje).nombre())
        personaje.regar()
        personaje.cosechar()
        assert.equals("Trigo", game.uniqueCollider(personaje).nombre())
        personaje.regar()
        personaje.cosechar()
        assert.that(game.colliders(personaje).isEmpty())
    }

    test "Test de cosecha: Tomaco" {
        personaje.sembrar(tomaco)
        personaje.cosechar()
        assert.that(game.colliders(personaje).isEmpty())
    }

    test "Test de cosecha: No hay cultivo" {
        assert.throwsException({ personaje.cosechar() })
    }

    test "Test de cosecha: La planta no puede cosecharse" {
        personaje.sembrar(trigo)
        personaje.cosechar()
        assert.notThat(game.colliders(personaje).isEmpty())
        assert.equals(0, personaje.plantasCosechadas().size())
    }
}

describe "Test de la venta de cultivos y los mercados" {
    var mercadoPrueba
    method initialize() {
        mercadoPrueba = new Mercado(position = game.at(0,0), monedas = 80)
    }
    test "Test oro total de venta de 1 maiz por 150 monedas" {
        personaje.sembrar(maiz)
        personaje.regar()
        personaje.cosechar()
        assert.equals(150, personaje.oroTotalPlantasCosechadas())
    }

    test "Test oro total de venta de 1 trigo etapa 2 por 100 monedas" {
        personaje.sembrar(trigo)
        personaje.regar()
        personaje.regar()
        personaje.cosechar()
        assert.equals(100, personaje.oroTotalPlantasCosechadas())
    }

    test "Test oro total de venta de 1 trigo etapa 3 por 200 monedas" {
        personaje.sembrar(trigo)
        personaje.regar()
        personaje.regar()
        personaje.regar()
        personaje.cosechar()
        assert.equals(200, personaje.oroTotalPlantasCosechadas())
    }

    test "Test oro total de venta de 1 tomaco por 80 monedas" {
        personaje.sembrar(tomaco)
        personaje.cosechar()
        assert.equals(80, personaje.oroTotalPlantasCosechadas())
    }

    test "Test de la venta y el mercado: Verificaciones - No se puede vender por falta de mercado" {
        personaje.sembrar(tomaco)
        personaje.cosechar()
        assert.throwsException({ personaje.vender()})
    }

    test "Test de la venta y el mercado: Verificaciones - No se puede vender porque el mercado no tiene monedas" {
        personaje.sembrar(maiz)
        personaje.regar()
        personaje.cosechar()
        game.addVisual(personaje)
        personaje.position(game.at(0, 0))
        game.addVisual(mercadoPrueba)
        personaje.vender()
        assert.equals(0, personaje.oroActual())
        assert.notThat(personaje.plantasCosechadas().isEmpty())
        assert.equals(80, mercadoPrueba.monedas())
        assert.that(mercadoPrueba.mercaderia().isEmpty())
    }

    test "Test de la venta y el mercado: Venta exitosa" {
        personaje.sembrar(tomaco)
        personaje.cosechar()
        game.addVisual(personaje)
        personaje.position(game.at(0, 0))
        game.addVisual(mercadoPrueba)
        personaje.vender()
        assert.equals(80, personaje.oroActual())
        assert.equals(#{}, personaje.plantasCosechadas())
        assert.equals(0, mercadoPrueba.monedas())
        assert.notThat(mercadoPrueba.mercaderia().isEmpty())
    }
}

describe "Test del aspersor y su funcionamiento" {
    var maizArribaIzquierda
    var maizArriba
    var maizArribaDerecha
    var maizIzquierda
    var maizDerecha
    var maizAbajoIzquierda
    var maizAbajo
    var maizAbajoDerecha
    var aspersorPrueba

    method initialize() {
        maizArribaIzquierda = new Maiz(position = game.at(4,5))
        maizArriba = new Maiz(position = game.at(5,5))
        maizArribaDerecha = new Maiz(position = game.at(6,5))
        maizIzquierda= new Maiz(position = game.at(4,4))
        maizDerecha= new Maiz(position = game.at(6,4))
        maizAbajoIzquierda = new Maiz(position = game.at(4,3))
        maizAbajo = new Maiz(position = game.at(5,3))
        maizAbajoDerecha= new Maiz(position = game.at(6,3))
        aspersorPrueba = new Aspersor(position = game.at(5, 4))
    }
    test "Test del aspersor - Verificaciones: Comprobar si la parcela est√° vacia en la posicion dada" {
        game.addVisual(maizArriba)
        assert.notThat(aspersorPrueba.esUnaParcelaVacia(game.at(5,5)))
        assert.that(aspersorPrueba.esUnaParcelaVacia(game.at(0,0)))
    }

    test "Test del aspersor - Verificaciones: Comprobar si hay elemento unico en la posicion dada" {
        game.addVisual(maizArriba)
        assert.that(aspersorPrueba.hayElementoUnicoEn(game.at(5,5)))
        const maizDuplicado = new Maiz(position = game.at(5, 5))
        game.addVisual(maizDuplicado)
        assert.notThat(aspersorPrueba.hayElementoUnicoEn(game.at(5,5)))
    }
    test "Test del aspersor - Regar" {
        game.addVisual(maizArriba)
        aspersorPrueba.regar(game.at(5,5))
        assert.equals(adult, maizArriba.estado())
    }
    test "Test del aspersor - Regar Limitrofes" {
        game.addVisual(maizArribaIzquierda)
        game.addVisual(maizArriba)
        game.addVisual(maizArribaDerecha)
        game.addVisual(maizIzquierda)
        game.addVisual(maizDerecha)
        game.addVisual(maizAbajoIzquierda)
        game.addVisual(maizAbajo)
        game.addVisual(maizAbajoDerecha)
        game.addVisual(aspersorPrueba)
        aspersorPrueba.regarLimitrofes()
        assert.equals(adult, maizArribaIzquierda.estado())
        assert.equals(adult, maizArriba.estado())
        assert.equals(adult, maizArribaDerecha.estado())
        assert.equals(adult, maizIzquierda.estado())
        assert.equals(adult, maizDerecha.estado())
        assert.equals(adult, maizAbajoIzquierda.estado())
        assert.equals(adult, maizAbajo.estado())
        assert.equals(adult, maizAbajoDerecha.estado())
    }
}